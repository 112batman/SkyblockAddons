plugins {
    id 'net.minecraftforge.gradle.forge' version '2.1-SNAPSHOT'
}

// The below line is for version checkers <= 1.4.2
//version = "1.5.2"

repositories {
    mavenCentral()
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
}

dependencies {
    // Discord RPC for Java https://github.com/jagrosh/DiscordIPC
    implementation('com.github.jagrosh:DiscordIPC:e29d6d8') {
        exclude module: 'log4j'
    }
}

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

minecraft {
    version = "${project.minecraftVersion}-${project.forgeVersion}"
    runDir = 'run'

    mappings = "${project.mappings}"

    // Replace placeholders in source code
    replace("@MOD_ID@": project.modId,
            "@MOD_NAME@": project.name,
            "@VERSION@": project.version,
            "@MOD_ACCEPTED@": project.acceptedMinecraftVersions)
    replaceIn("${project.name}.java")
}

shadowJar {
    manifest.attributes(
            'FMLCorePlugin': "${project.group}.${project.modId}.tweaker.${project.name}LoadingPlugin",
            'ForceLoadAsMod': true,
            'FMLCorePluginContainsFMLMod': true,
            'ModSide': 'CLIENT',
            'FMLAT': "${project.modId}_at.cfg"
    )
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}